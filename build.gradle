plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.11.0'
}


repositories {
    mavenCentral()
}

javafx {
    modules = ['javafx.controls']
}

application {
    mainClassName = "org.example.hellofx.Launcher"
    applicationName = 'hellofx'
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        unixScriptTemplate = file('templates/unixScriptTemplate.txt')
        windowsScriptTemplate = file('templates/windowsScriptTemplate.txt')
    }
    jpackage {
        if(org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
    }
}

tasks.runtime.doLast {
    exec {
        commandLine = [
            "$imageDir/bin/java", "-Xshare:dump"
        ]
    }
}
